plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'de.undercouch.download' version '4.1.1'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'dev.rodrick'
version plugin_version

bukkit {
    // See https://github.com/Minecrell/plugin-yml other options

    // Change name and version in gradle.properties
    name = plugin_name
    version = plugin_version
    author = 'Rodrigo Addor <rodrigotladdor@gmail.com>'
    website = 'https://github.com/rodrigoaddor/musix'

    main = 'dev.rodrick.musix.MusixPlugin'
    apiVersion = '1.16'
}

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = "https://raw.githubusercontent.com/JorelAli/CommandAPI/mvn-repo/" }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    api "org.spigotmc:spigot-api:$minecraft_version-$spigot_version"
    implementation 'dev.jorel:commandapi-shade:5.8'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.7.1'
}

shadowJar {
    archiveBaseName.set(plugin_name)
    archiveClassifier.set('')
}

task downloadServer(type: Download) {
    src "https://papermc.io/api/v1/paper/$minecraft_version/latest/download"
    dest "$rootDir/server/paper.jar"
    onlyIfModified true
}

task copyPlugin(type: Copy) {
    dependsOn('shadowJar')
    from shadowJar.archiveFile
    into "$rootDir/server/plugins"
    rename '(.+)-\\d+.\\d+', '$1'
}

task runServer(type: JavaExec) {
    dependsOn('downloadServer')
    dependsOn('copyPlugin')
    standardInput = System.in
    classpath = files("$rootDir/server/paper.jar")
    workingDir = "$rootDir/server"
    maxHeapSize '3G'
    args 'nogui'
}
